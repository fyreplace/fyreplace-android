plugins {
    id 'com.android.application'
    id 'com.google.protobuf'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk configuration.compileSdk

    defaultConfig {
        applicationId "app.fyreplace.client"
        minSdk configuration.minSdk
        targetSdk configuration.targetSdk
        versionCode 9001
        versionName "0.9.1"
        vectorDrawables.useSupportLibrary true
    }

    buildTypes {
        debug {
            versionNameSuffix "-dev"
            applicationIdSuffix ".dev"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "freedom"
    productFlavors {
        vanilla {
            dimension "freedom"
        }

        libre {
            dimension "freedom"
        }
    }

    buildFeatures {
        dataBinding true
    }
}

protobuf {
    protoc {
        artifact = libraries.protoc
    }

    plugins {
        grpc {
            artifact = libraries.grpc.protoc
        }

        grpckt {
            artifact = libraries.grpc.protocKotlin
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
                kotlin {}
            }

            task.plugins {
                grpc {}
                grpckt {}
            }
        }
    }
}

dependencies {
    kapt processors.glide
    implementation libraries.kotlinx.coroutines
    implementation libraries.androidx.appCompat
    implementation libraries.androidx.constraintLayout
    implementation libraries.androidx.coordinatorLayout
    implementation libraries.androidx.core
    implementation libraries.androidx.fragment
    implementation libraries.androidx.lifecycle.runtime
    implementation libraries.androidx.lifecycle.viewModel
    implementation libraries.androidx.navigation.fragment
    implementation libraries.androidx.navigation.ui
    implementation libraries.androidx.paging
    implementation libraries.androidx.preference
    implementation libraries.androidx.security
    implementation libraries.androidx.swipeRefreshLayout
    implementation libraries.glide.base
    implementation libraries.glide.okHttp
    implementation libraries.grpc.okHttp
    implementation libraries.grpc.protobuf
    implementation libraries.grpc.stub
    implementation libraries.grpc.stubKotlin
    implementation libraries.javax.annotation
    implementation libraries.koin
    implementation libraries.material
    implementation libraries.protobuf
    vanillaImplementation libraries.play.basement
    libreImplementation libraries.conscrypt
}
