plugins {
    id 'com.android.application'
    id 'com.google.protobuf'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion configuration.compileSdkVersion

    defaultConfig {
        applicationId "app.fyreplace.client"
        minSdkVersion configuration.minSdkVersion
        targetSdkVersion configuration.targetSdkVersion
        versionCode 9001
        versionName "0.9.1"
        vectorDrawables.useSupportLibrary true
    }

    buildTypes {
        debug {
            versionNameSuffix "-dev"
            applicationIdSuffix ".dev"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "freedom"
    productFlavors {
        vanilla {
            dimension "freedom"
        }

        libre {
            dimension "freedom"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding true
    }
}

protobuf {
    protoc {
        artifact = libraries.protoc
    }

    plugins {
        grpc {
            artifact = libraries.grpc.protoc
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins { java {} }
            task.plugins { grpc {} }
        }
    }
}

dependencies {
    implementation libraries.kotlin
    implementation libraries.androidx.appCompat
    implementation libraries.androidx.core
    implementation libraries.androidx.fragment
    implementation libraries.androidx.lifecycle.liveData
    implementation libraries.androidx.lifecycle.runtime
    implementation libraries.androidx.lifecycle.viewModel
    implementation libraries.androidx.navigation.fragment
    implementation libraries.androidx.navigation.ui
    implementation libraries.androidx.security
    implementation libraries.grpc.okHttp
    implementation libraries.grpc.protobuf
    implementation libraries.grpc.stub
    implementation libraries.javax.annotation
    implementation libraries.koin
    implementation libraries.material
    vanillaImplementation libraries.play.basement
    libreImplementation libraries.conscrypt
}
